plugins {
    id 'java'
    id 'eclipse'
    id 'ca.coglinc2.javacc' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def javaVersion = JavaLanguageVersion.of(8)
def javaHome = javaToolchains.compilerFor { languageVersion = javaVersion }.get().metadata.installationPath.asFile.toString()

repositories {
    mavenCentral()
    flatDir {
    	dirs "${javaHome}/lib"
    }
}

java {
    toolchain {
        languageVersion = javaVersion
    }
}

dependencies {
    implementation group: 'org.javassist', name:'javassist', version:'3.29.1-GA'
    implementation name: 'tools'
    testImplementation 'junit:junit:4.13.2'
}

group = 'jbse'
version = '0.11.0-SNAPSHOT'
description = 'JBSE'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJavacc {
    arguments = [output_directory: compileJavacc.outputDirectory.absolutePath + '/jbse/apps/settings']
}

sourceSets {
    main {
        java {
            srcDir compileJavacc.outputDirectory
        }
    }
}

task copyToLib(type: Copy) {
    into "deps"
    from configurations.compileClasspath
}

shadowJar {
    archiveClassifier = 'shaded'
    exclude 'tools.jar'
    relocate 'javassist', 'jbse.javassist'
}

build.dependsOn(copyToLib)

build.dependsOn 'shadowJar'

jar {
    manifest {
        attributes 'Specification-Title': 'Java Virtual Machine Specification',
                   'Specification-Vendor': 'Oracle Corporation',
                   'Specification-Version': '1.8',
                   'Implementation-Title': description,
                   'Implementation-Vendor': 'University of Milano-Bicocca',
                   'Implementation-Version': archiveVersion
    }
}
